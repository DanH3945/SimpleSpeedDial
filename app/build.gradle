apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.danh3945.simplespeeddial"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']

        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        // Added compat for Java 1.8  LAMBDAS!!!
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions 'versions'
    productFlavors {
        free {
            dimension = 'versions'
            signingConfig signingConfigs.release
        }
        paid {
            dimension = 'versions'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifeCycleVersion"
    implementation "androidx.preference:preference:$rootProject.preferenceVersion"

    implementation "androidx.recyclerview:recyclerview:$rootProject.androidXBaseVersion"
    implementation "androidx.cardview:cardview:$rootProject.androidXBaseVersion"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.androidXBaseVersion"
    implementation "androidx.vectordrawable:vectordrawable-animated:$rootProject.androidXBaseVersion"
    implementation "androidx.exifinterface:exifinterface:$rootProject.androidXBaseVersion"
    implementation "androidx.browser:browser:$rootProject.androidXBaseVersion"

    // Google Play and Firebase
    implementation "com.google.android.gms:play-services-base:$rootProject.playServicesVersion"
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseCoreVersion"
    implementation "com.google.firebase:firebase-ads:$rootProject.firebaseAdVersion"

    // Google Play Billing
    implementation "com.android.billingclient:billing:$rootProject.googlePlayBillingVersion"

    // Third Party Libraries
    implementation "de.psdev.licensesdialog:licensesdialog:$rootProject.licenseDialogVersion"
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // Room database libraries
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"

    // Gson
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // TextDrawable library for drawing default icons in the Android style.
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'org.jetbrains:annotations-java5:15.0'
}

apply plugin: 'com.google.gms.google-services'
